import tart.core.Memory.zeroPtr;
import tart.core.Memory.addressOf;
import tart.core.Memory.ptrDiff;
import tart.core.Math.min;

/** Built-in array class */
public final class Array[%ElementType] : Iterable[ElementType] {
  private {
    var end:NativePointer[ElementType];
    var data:NativeArray[ElementType, 0];

    /** Array allocator function */
    static def alloc(length:int) -> Array {
      //InvalidArgumentException.when(length < 0);
      // The 'offsetof' trick for calculating the size.
      let self:Array = __pvalloc(zeroPtr[Array[ElementType]]().data[length]);
      self.end = addressOf(self.data[length]);
      return self;
    }

    //def asBuffer() {
    //  return MemoryBuffer.<ElementType>(self, addressOf(self.data[0]), end);
    //}
  }

  /** Construct an array of a given length */
  static def create(length:int) -> Array {
    let self:Array = alloc(length);
    // TODO: Need to initialize the array elements to default values.
    // Memory.initToDefaultValues(addressOf(data[0]), end);
    return self;
  }

  /** Static factory function which takes advantage of the built-in behavior of
      variadic parameters to build an Array. */
  static def of(elements:ElementType...) -> Array {
    return elements;
  }

  /** True if the array is zero length. */
  def isEmpty:bool {
    get { return self.end == addressOf(self.data[0]); }
  }

  /** The length of the array. */
  def length:int {
    get { return ptrDiff(addressOf(self.data[0]), self.end); }
  }

  /** Array element access. */
  def [index:int]:ElementType {
    get {
      Preconditions.checkIndex(index >= 0);
      return self.data[index];
    }
    set {
      Preconditions.checkArgument(index >= 0);
      self.data[index] = value;
    }
  }
  
  /** Iterate over the elements of the array. */
  def iterate -> Iterator[ElementType] {
    return ArrayIterator(self);
  }

  /** Copy 'count' elements, starting from offset 'srcOffset' in array 'src', to offset
      'dstOffset' in array 'dst'.
   */  
  static def copy(dst:Array, dstOffset:int, src:Array, srcOffset:int, count:int) {
    Preconditions.checkIndex(count >= 0);
    Preconditions.checkIndex(dstOffset >= 0 and dstOffset <= dst.length);
    Preconditions.checkIndex(srcOffset >= 0 and srcOffset <= src.length);
    count = min(count, dst.length - dstOffset, src.length - srcOffset);
    if count > 0 {
      Memory.arrayMove(dst.data, dstOffset, src.data, srcOffset, count);
    }
  }

  /** Copy 'count' elements, starting from offset 'srcOffset' in array 'src', to offset
      'dstOffset' in this array.
   */  
  static def copy(dst:Array, dstOffset:long, src:Array, srcOffset:long, count:long) {
    Preconditions.checkIndex(count >= 0);
    Preconditions.checkIndex(dstOffset >= 0 and dstOffset <= dst.length);
    Preconditions.checkIndex(srcOffset >= 0 and srcOffset <= src.length);
    count = min(count, dst.length - dstOffset, src.length - srcOffset);
    if count > 0 {
      Memory.arrayMove(dst.data, dstOffset, src.data, srcOffset, count);
    }
  }

  public final class ArrayIterator : Iterator[ElementType], HasLength {
    private var array:Array;
    private var index:int;
    
    def construct(array:Array) {
      self.array = array;
      self.index = 0;
    }
    
    def next() -> ElementType or void {
      if (self.index < self.array.length) {
        return self.array.data[self.index++];
      } else {
        return;
      }
    }

    def length:long { get { return array.length; } }
  }
}
