import tart.core.Memory.zeroPtr;
import tart.core.Memory.addressOf;
import tart.core.Memory.ptrDiff;

/** Built-in array class */
public final class Array[%ElementType] : Iterable[ElementType] {
  private {
    var end:NativePointer[ElementType];
    var data:NativeArray[ElementType, 0];

    /** Array allocator function */
    static def alloc(length:int) {
      //InvalidArgumentException.when(length < 0);
      // The 'offsetof' trick for calculating the size.
      let self:Array = __pvalloc(zeroPtr[Array[ElementType]]().data[length]);
      self.end = addressOf(self.data[length]);
      return self;
    }

    //def asBuffer() {
    //  return MemoryBuffer.<ElementType>(self, addressOf(self.data[0]), end);
    //}
  }

  /** Construct an array of a given length */
  static def create(length:int) -> Array {
    let self:Array = alloc(length);
    // TODO: Need to initialize the array elements to default values.
    // Memory.initToDefaultValues(addressOf(data[0]), end);
    return self;
  }

  /** Static factory function which takes advantage of the built-in behavior of
      variadic parameters to build an Array. */
  static def of(elements:ElementType...) {
    return elements;
  }

  /** True if the array is zero length. */
  def isEmpty:bool {
    get { return self.end == addressOf(self.data[0]); }
  }

  /** The length of the array. */
  def length:int {
    get { return ptrDiff(addressOf(self.data[0]), self.end); }
  }

  /** Array element access. */
  def [index:int]:ElementType {
    get { return self.data[index]; }
    set { self.data[index] = value; }
  }
  
  /** Iterate over the elements of the array. */
  def iterate -> Iterator[ElementType] {
    return ArrayIterator(self);
  }

  public final class ArrayIterator : Iterator[ElementType], HasLength {
    private var array:Array;
    private var index:int;
    
    def construct(array:Array) {
      self.array = array;
      self.index = 0;
    }
    
    def next() -> ElementType or void {
      if (self.index < self.array.length) {
        return self.array.data[self.index++];
      } else {
        return;
      }
    }

    def length:long { get { return 0; } }
  }
}
