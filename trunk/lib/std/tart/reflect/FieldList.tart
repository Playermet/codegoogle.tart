import tart.collections.List;
import tart.core.Memory.Address;
import tart.gc.TraceMethod;
import tart.gc.TraceAction;

/** An immutable list of attributes that points to a statically-initialized array.
    InheritDoc: members
*/
final class FieldList : List[Field] {
  private {
    var _size:int;
    var _data:FlexibleArray[Field];

    /** Custom trace method for garbage collection to handle variable-length object. */
    @TraceMethod def __trace(action:TraceAction) {
      for i = 0; i < _size; ++i {
        action.trace(_data[i]);
      }
    }
  }

  // TODO: Make this undef. (Undefs should automatically throw UnsupportedOperationError().) */
  def add(e:Field) {
    throw UnsupportedOperationError();
  }

  def insert(position:int, e:Field) {
    throw UnsupportedOperationError();
  }

  def remove(index:int) {
    throw UnsupportedOperationError();
  }

  def [index:int]:Field {
    get {
      return _data[index];
    }

    set {
      throw UnsupportedOperationError();
    }
  }

  def clear {
    throw UnsupportedOperationError();
  }

  def size:int {
    get { return self._size; }
  }

  def isEmpty:bool {
    get { return self._size == 0; }
  }

  def contains(type:Field) -> bool {
    for i = 0; i < _size; ++i {
      if self._data[i] == type {
        return true;
      }
    }
    return false;
  }

  def iterate -> Iterator[Field] {
    return IteratorImpl(self);
  }

  private final class IteratorImpl : Iterator[Field], HasLength {
    private var _typeList:FieldList;
    private var _index:int;

    def construct(typeList:FieldList) {
      self._typeList = typeList;
      self._index = 0;
    }

    def next() -> Field or void {
      if (self._index < self._typeList._size) {
        return self._typeList._data[self._index++];
      } else {
        return;
      }
    }

    def length:int { get { return _typeList._size; } }
  }
}
