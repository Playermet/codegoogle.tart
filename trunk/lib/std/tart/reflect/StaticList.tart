import tart.collections.Collection;
import tart.collections.List;
import tart.core.Memory.Address;
import tart.gc.TraceMethod;
import tart.gc.TraceAction;

/** An immutable list of items that points to a statically-initialized array.
    InheritDoc: members
    TODO: Make immutable.
 */
class StaticList[%T] : List[T] {
  private {
    var _size:int;
    var _data:FlexibleArray[T];

    /** Custom trace method for garbage collection to handle variable-length object. */
    @TraceMethod def __trace(action:TraceAction) {
      for i = 0; i < _size; ++i {
        action.trace(_data[i]);
      }
    }
  }

  undef append(e:T);
  undef appendAll(e:Collection[T]);
  undef insert(position:int, e:T);
  undef insertAll(position:int, e:Collection[T]);
  undef replace(index:int, count:int, src:Collection[T]);
  undef remove(index:int);
  undef clear;

  final def [index:int]:T {
    get {
      return _data[index];
    }

    set {
      throw UnsupportedOperationError();
    }
  }

  final def size:int {
    get { return self._size; }
  }

  final def isEmpty:bool {
    get { return self._size == 0; }
  }

  final def contains(type:T) -> bool {
    for i = 0; i < _size; ++i {
      if self._data[i] == type {
        return true;
      }
    }
    return false;
  }

  final def iterate -> Iterator[T] {
    return IteratorImpl(self);
  }

  private final class IteratorImpl : Iterator[T], HasLength {
    private var _typeList:StaticList;
    private var _index:int;

    def construct(typeList:StaticList) {
      self._typeList = typeList;
      self._index = 0;
    }

    def next() -> T or void {
      if (self._index < self._typeList._size) {
        return self._typeList._data[self._index++];
      } else {
        return;
      }
    }

    def length:int { get { return _typeList._size; } }
  }
}
