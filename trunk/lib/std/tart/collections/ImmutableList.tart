import tart.core.Math.max;
import tart.core.Memory.arrayCopy;
import tart.core.Memory.Address;

/** A list type with a fixed set of elements.
    InheritDoc: members
 */
final class ImmutableList[%ElementType] : List[ElementType], Copyable[ElementType] {
  private {
    var data:ElementType[];
  }

  /** Create an empty ImmutableList. */
  private def construct() {
    self.data = [];
  }

  /** Create an ImmutableList from the elements given. */
  private def construct(data:ElementType[]) {
    self.data = data;
  }

  undef append(e:ElementType) ;
  undef appendAll(collection:Collection[ElementType]);
  undef insert(position:int, e:ElementType);
  undef insertAll(position:int, collection:Collection[ElementType]);
  undef replace(index:int, count:int, src:Collection[ElementType]);
  undef remove(index:int);
  undef clear();

  def [index:int]:ElementType {
    get {
      return data[index];
    }

    set {
      throw UnsupportedOperationError();
    }
  }

  def size:int {
    get { return self.data.size; }
  }

  def isEmpty:bool {
    get { return self.data.size == 0; }
  }

  def contains(el:ElementType) -> bool {
    for i = 0; i < self.data.size; ++i {
			if self.data[i] == el {
			  return true;
			}
    }
    return false;
  }

  readonly def iterate -> Iterator[ElementType] {
    return self.data.iterate();
  }

  def copyElements(dstAddr:Address[ElementType], srcOffset:int = 0, count:int = int.maxVal) {
    self.data.copyElements(dstAddr, srcOffset, count);
  }

  /** Construct a new 'ImmutableList' from a variable number of elements. */
  static def of(elements:ElementType...) -> ImmutableList {
    return ImmutableList(elements);
  }

  /** Construct a new 'ImmutableList' from a array ofelements. */
  static def copyOf(elements:ElementType[]) -> ImmutableList {
    return ImmutableList(Array.copyOf(elements));
  }

  /** Construct a new 'ImmutableList' from a collection. */
  static def copyOf(elements:Collection[ElementType]) -> ImmutableList {
    return ImmutableList(Array.copyOf(elements));
  }
}
