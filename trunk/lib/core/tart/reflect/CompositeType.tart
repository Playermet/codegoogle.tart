import tart.annex.Intrinsic;
import tart.core.Memory.Address;
import tart.core.Memory.reinterpretPtr;
import tart.core.Memory.bitCast;

/** Reflection type for classes, structs, and interfaces. */
public class CompositeType : Type {
  struct Data {
    let streamData:Address[ubyte];
    let typeRefs:Address[Type];
    let globalRefs:Address[Address[void]];
    let methodBaseIndex:int16;
  }

  private {
    let _meta:ReflectionMetadata;
    var _names:NameTable;
    var _nameIndex:int16;
    var _size:uint;
    let _typeInfo:Address[TypeInfoBlock];
    var _supertype:optional CompositeType;
    var _interfaces:CompositeType[];
    var _typeParams:Type[];
    var _attributes:Object[];
    var _fields:optional Field[];
    var _properties:optional Property[];
    var _constructors:optional Method[];
    var _methods:optional Method[];
    var _innerTypes:Type[];
    var _alloc:static fn -> Object;
    var _noArgCtor:static fn :Object -> Object;
    // Need type adapter functions for structs.
  }

  def construct(typeKind:TypeKind, typeInfo:Address[TypeInfoBlock], meta:ReflectionMetadata) {
    super(typeKind);
    self._typeInfo = typeInfo;
    self._meta = meta;
  }

  def construct(typeKind:TypeKind, typeInfo:Address[TypeInfoBlock], baseTemplate:CompositeType, meta:ReflectionMetadata) {
    super(typeKind);
    self._typeInfo = typeInfo;
    self._meta = meta;
    // TODO: Assign base template
  }

  /** Given a type literal, return the reflected complex type object. */
  @Intrinsic static def of[%T](typeName:TypeLiteral[T]) -> CompositeType;

  /** Fully-qualified name of this type. */
  final def name:String { get { return _names.getLocalName(_nameIndex); } }

  /** Fully-qualified name of this type. */
  final def qualifiedName:String { get { return _names.getQualifiedName(_nameIndex); } }

  /** Fully-qualified name of the scope in which this type is defined. */
  final def scopeName:String { get { return _names.getScopeName(_nameIndex); } }

  /** Size of this type in bytes. */
  final def size:uint { get { return _size; } }

	/** TypeInfoBlock for this type. */
  final def typeInfo:Address[TypeInfoBlock] { get { return _typeInfo; } }

  /** Pointer to this type's supertype, or null. */
  final def supertype:optional CompositeType {
    get {
      if _supertype is null {
        self._supertype = _meta.readBaseClass(self);
      }

      return _supertype;
  	}
  }

  /** Array of inherited interfaces. */
  final def interfaces:CompositeType[] { get { return self._interfaces; } }

  /** Array of type parameters. */
  final def typeParams:Type[] { get { decodeTypeParams(); return self._typeParams; } }

  /** Array of associated attributes */
  final def attributes:Object[] { get { return self._attributes; } }

  /** Array of field members. */
  final def fields:Field[] { get { return self._fields; } }

  /** Array of property members. */
  final def properties:Property[] { get { return self._properties; } }

  /** Array of constructor members. */
  final def constructors:Method[] { get { decodeMethods(); return _constructors; } }

  /** Array of methods. */
  final def methods:Method[] { get { decodeMethods(); return _methods; } }

  /** Array of types defined within this type. */
  final def innerTypes:Type[] { get { return self._innerTypes; } }

  /** Return true if this type has a custom allocator. */
  final def hasCustomAlloc:bool { get { return self._alloc is null; } }

  /** Return true if this type has a no-arg constructor. */
  final def hasNoArgConstructor:bool { get { return self._noArgCtor is not null; } }

	/** If 'obj' is castable to this type, do the cast and return the raw address,
			otherwise throw a typecast exception. */
  @Unsafe final override downCast(obj:Object) -> Address[void] {
    if obj is not null {
    	if obj.__typeHasBase(_typeInfo) {
    	  return Memory.reinterpretPtr(Memory.objectAddress(obj));
    	}
    }
   	throw TypecastError(obj, self);
  }

  @Unsafe final override checkCast(obj:Object) -> Object {
    if obj is not null {
    	if obj.__typeHasBase(_typeInfo) {
    	  return obj;
    	}
    }
   	throw TypecastError(obj, self);
  }

  /** Return the first method whose name is 'name'. */
  def findMethod(name:String) -> optional Method {
    for m in methods {
      if m.name == name {
        return m;
      }
    }

    return null;
  }

  /** Allocate and initialize a new object of this type, using the specified constructor. */
  final def create(ctor:Method, args:Object[]) -> Object {
    switch (typeKind) {
      case CLASS {
        let instance = _meta._alloc();
        ctor.call(instance, args);
        return instance;
      }

      case STRUCT {
        throw InvocationError("Unimplemented struct construction");
      }

      else {
        throw InstantiationError(self);
      }
    }
  }

  /** Allocate and initialize a new object of this type, using the specified constructor. */
  final def create(ctor:Method, args:Object...) -> Object {
    return create(ctor, args);
  }

  /** Allocate and initialize a new object of this type, using a default constructor. */
  final def create() -> Object {
    switch (typeKind) {
      case CLASS {
	      if _noArgCtor is null {
  	      for ctor in constructors {
  	        if ctor.numParams == 0 {
			        let instance = _meta._alloc();
              ctor.call(instance);
              return instance;
  	        }
  	      }
	      }

	      if _noArgCtor is not null {
	        let instance = self._alloc();
	        _noArgCtor(instance);
	        return instance;
	      }

        throw InvocationError("Class '{0}' does not have a no-arg constructor".format(qualifiedName));
      }

      case STRUCT {
        throw InvocationError("Unimplemented struct construction");
      }

      else {
        throw InstantiationError(self);
      }
    }
  }

  final def isSubclass(cls:Type) -> bool {
    return isSubclassImpl(cls);
  }

  final def isSubclass[%T](cls:TypeLiteral[T]) -> bool {
    return isSubclassImpl(Type.of(T));
  }

  private final def isSubclassImpl(cls:Type) -> bool {
    return true if self is cls;
    classify cls as complex:CompositeType {
      return Memory.deref(_typeInfo).hasBase(complex._typeInfo);
    } else {
      return false;
    }
  }

  /** Return the first attribute of type 'attrType'. */
  final def findAttribute[%T](attrType:TypeLiteral[T]) -> optional T {
    for attr in _attributes {
      classify attr as result:T {
        return result;
      }
    }

    return null;
  }

  final override toString -> String {
    // TODO: Should have a flag indicating whether this is a template instance, template, etc.
    var params = typeParams;
    if not params.isEmpty {
	    let sb = StringBuilder(qualifiedName);
      let typeStrings = tart.collections.ArrayList[String]();
      for type in params {
        typeStrings.add(type.toString());
      }

      sb.append(String.format("[{0}]", ",".join(typeStrings)));
	    return sb.toString();
    } else {
	    return qualifiedName;
    }
  }

  private final def decodeMethods {
    if self._methods is null {
	    self._constructors, self._methods = _meta.readClassMethods(self);
    }
  }

  private final def decodeTypeParams {
    if self._typeParams is null {
	    self._typeParams = _meta.readTypeParams();
    }
  }

  @Unsafe final override getFieldValue(fieldAddr:Address[void]) -> Object {
    switch typeKind {
    	case TypeKind.CLASS
    	case TypeKind.INTERFACE {
		    let addr:Address[Object] = Memory.reinterpretPtr(fieldAddr);
  		  return addr[0];
    	}

    	case TypeKind.STRUCT {
	      // TODO: Implement
  	    return self;
    	}
    	
    	else {
	      // TODO: throw something
  	    return self;
    	}
    }
  }

  @Unsafe final override setFieldValue(fieldAddr:Address[void], value:Object) {
    switch typeKind {
    	case TypeKind.CLASS
    	case TypeKind.INTERFACE {
		    let addr:Address[Object] = Memory.reinterpretPtr(fieldAddr);
  		  addr[0] = self.checkCast(value);
    	}

    	case TypeKind.STRUCT {
	      // TODO: Implement
    	}
    	
    	else {
	      // TODO: throw something
    	}
	  }
  }

  @Unsafe final override getPropertyValue(obj:Object, getter:Address[void]) -> Object {
    switch typeKind {
    	case TypeKind.CLASS
    	case TypeKind.INTERFACE {
    		let getterFn:static fn (obj:Object) -> Object = bitCast(getter);
		    return getterFn(obj);
    	}

    	else {
	      // TODO: Implement
		    return self;
    	}
	  }
  }

  @Unsafe final override setPropertyValue(obj:Object, setter:Address[void], value:Object) {
    switch typeKind {
    	case TypeKind.CLASS
    	case TypeKind.INTERFACE {
		    let setterFn:static fn (obj:Object, value:Object) = bitCast(setter);
    		setterFn(obj, self.checkCast(value));
    	}

    	else {
	      // TODO: Implement
    	}
	  }
  }

  def test() {
    // TODO: For some reason deleting this line makes things break, even though it's never called.
 	  _meta.readClassMethods(self);
  }
}
