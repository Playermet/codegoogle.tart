import tart.annex.Intrinsic;
import tart.collections.List;
import tart.collections.ArrayList;
import tart.collections.ImmutableList;
import tart.core.Memory.Address;
import tart.core.Memory.reinterpretPtr;
import tart.core.Memory.bitCast;
import tart.core.Memory.addressOf;

/** Reflection type for classes, structs, and interfaces. */
public class CompositeType : Type {
  private {
    var _names:NameTable;
    var _nameIndex:int16;
    var _size:uint;
    let _typeInfo:Address[TypeInfoBlock];
    var _strmData:Address[ubyte];
    var _typeRefs:Address[Type];
    var _globalRefs:Address[Object];
    var _methodRefs:Address[Address[void]];
    var _supertype:optional CompositeType;
    var _interfaces:List[Type];
    var _typeParams:List[Type];
    var _attributes:List[Object];
    var _fields:List[Field];
    var _properties:List[Property];
    var _constructors:optional List[Method];
    var _methods:optional List[Method];
    var _memberTypes:List[Type];
    var _alloc:static fn -> Object;
    var _noArgCtor:static fn :Object -> Object;
    var _methodBaseIndex:int;
    // Need type adapter functions for structs.
  }

  def construct(typeKind:TypeKind, typeInfo:Address[TypeInfoBlock]) {
    super(typeKind);
    self._typeInfo = typeInfo;
  }

  def construct(typeKind:TypeKind, typeInfo:Address[TypeInfoBlock], baseTemplate:CompositeType) {
    super(typeKind);
    self._typeInfo = typeInfo;
  }

  /** Given a type literal, return the reflected complex type object. */
  @Intrinsic static def of[%T](typeName:TypeLiteral[T]) -> CompositeType;

  /** Fully-qualified name of this type. */
  final def name:String { get { return _names.getLocalName(_nameIndex); } }

  /** Fully-qualified name of this type. */
  final def qualifiedName:String { get { return _names.getQualifiedName(_nameIndex); } }

  /** Fully-qualified name of the scope in which this type is defined. */
  final def scopeName:String { get { return _names.getScopeName(_nameIndex); } }

  /** Size of this type in bytes. */
  final def size:uint { get { return _size; } }

	/** TypeInfoBlock for this type. */
  final def typeInfo:Address[TypeInfoBlock] { get { return _typeInfo; } }

  /** Pointer to this type's supertype, or null. */
  final def supertype:optional CompositeType { get { return _supertype; } }

  /** Array of inherited interfaces. */
  final def interfaces:List[Type] { get { return self._interfaces; } }

  /** Array of type parameters. */
  final def typeParams:List[Type] { get { return self._typeParams; } }

  /** Array of associated attributes */
  final def attributes:List[Object] { get { return self._attributes; } }

  /** Array of field members. */
  final def fields:List[Field] { get { return self._fields; } }

  /** Array of property members. */
  final def properties:List[Property] { get { return self._properties; } }

  /** Array of constructor members. */
  final def constructors:List[Method] { get { decodeMethods(); return _constructors; } }

  /** Array of methods. */
  final def methods:List[Method] { get { decodeMethods(); return _methods; } }

  /** Array of types defined within this type. */
  final def memberTypes:List[Type] { get { return self._memberTypes; } }

  /** Return true if this type has a custom allocator. */
  final def hasCustomAlloc:bool { get { return self._alloc is null; } }

  /** Return true if this type has a no-arg constructor. */
  final def hasNoArgConstructor:bool { get { return self._noArgCtor is not null; } }

	/** If 'obj' is castable to this type, do the cast and return the raw address,
			otherwise throw a typecast exception. */
  @Unsafe final override downCast(obj:Object) -> Address[void] {
    if obj is not null {
    	if obj.__typeHasBase(_typeInfo) {
    	  return Memory.reinterpretPtr(Memory.objectAddress(obj));
    	}
    }
   	throw TypecastError(obj, self);
  }

  @Unsafe final override checkCast(obj:Object) -> Object {
    if obj is not null {
    	if obj.__typeHasBase(_typeInfo) {
    	  return obj;
    	}
    }
   	throw TypecastError(obj, self);
  }

  /** Return the first method whose name is 'name'. */
  def findMethod(name:String) -> optional Method {
    for m in methods {
      if m.name == name {
        return m;
      }
    }

    return null;
  }

  /** Allocate and initialize a new object of this type, using the specified constructor. */
  final def create(ctor:Method, args:Object[]) -> Object {
    switch (typeKind) {
      case CLASS {
        let instance = _alloc();
        ctor.call(instance, args);
        return instance;
      }

      case STRUCT {
        throw InvocationError("Unimplemented struct construction");
      }

      else {
        throw InstantiationError(self);
      }
    }
  }

  /** Allocate and initialize a new object of this type, using the specified constructor. */
  final def create(ctor:Method, args:Object...) -> Object {
    return create(ctor, args);
  }

  /** Allocate and initialize a new object of this type, using a default constructor. */
  final def create() -> Object {
    switch (typeKind) {
      case CLASS {
	      if _noArgCtor is not null {
	        let instance = self._alloc();
	        _noArgCtor(instance);
	        return instance;
	      }

        throw InvocationError("Class '{0}' does not have a no-arg constructor".format(qualifiedName));
      }

      case STRUCT {
        throw InvocationError("Unimplemented struct construction");
      }

      else {
        throw InstantiationError(self);
      }
    }
  }

  final def isSubclass(cls:Type) -> bool {
    return isSubclassImpl(cls);
  }

  final def isSubclass[%T](cls:TypeLiteral[T]) -> bool {
    return isSubclassImpl(Type.of(T));
  }

  private final def isSubclassImpl(cls:Type) -> bool {
    return true if self is cls;
    match cls as complex:CompositeType {
      return Memory.deref(_typeInfo).hasBase(complex._typeInfo);
    } else {
      return false;
    }
  }

  /** Return the first attribute of type 'attrType'. */
  final def findAttribute[%T](attrType:TypeLiteral[T]) -> optional T {
    for attr in _attributes {
      match attr as result:T {
        return result;
      }
    }

    return null;
  }

  final override toString -> String {
    // TODO: Should have a flag indicating whether this is a template instance, template, etc.
    if not _typeParams.isEmpty {
	    let sb = StringBuilder(qualifiedName);
      let typeStrings = tart.collections.ArrayList[String]();
      for type in _typeParams {
        typeStrings.add(type.toString());
      }

      sb.append(String.format("[{0}]", ",".join(typeStrings)));
	    return sb.toString();
    } else {
	    return qualifiedName;
    }
  }

	/** Decode the compressed method descriptors. */
  private final def decodeMethods {
    if self._methods is null {
      if _strmData is null {
		    self._methods = ImmutableList.of();
		    self._constructors = ImmutableList.of();
      } else {
        let decoder = MemberDecoder(
        		_strmData, _names, _typeRefs, _globalRefs, _methodRefs,
        		addressOf(_typeInfo.methodTable[0]), _methodBaseIndex);
  	    let length = decoder.seekToSection(MemberDecoder.SectionTag.METHODS);
    	  let methods = ArrayList[Method]();
      	let constructors = ArrayList[Method]();
  	    if length != 0 {
  	      let endPos = decoder.position + length;
    	    while decoder.position < endPos {
      	    let m = decoder.decodeMethod();
        	  if m.kind == Member.MemberKind.CONSTRUCTOR {
          	  constructors.add(m);
          	} else {
            	methods.add(m);
          	}
        	}
  	    }
  	    self._methods = ImmutableList.copyOf(methods);
  	    self._constructors = ImmutableList.copyOf(constructors);
      }
    }
  }

  @Unsafe final override getFieldValue(fieldAddr:Address[void]) -> Object {
    switch typeKind {
    	case TypeKind.CLASS
    	case TypeKind.INTERFACE {
		    let addr:Address[Object] = Memory.reinterpretPtr(fieldAddr);
  		  return addr[0];
    	}

    	case TypeKind.STRUCT {
	      // TODO: Implement
  	    return self;
    	}
    	
    	else {
	      // TODO: throw something
  	    return self;
    	}
    }
  }

  @Unsafe final override setFieldValue(fieldAddr:Address[void], value:Object) {
    switch typeKind {
    	case TypeKind.CLASS
    	case TypeKind.INTERFACE {
		    let addr:Address[Object] = Memory.reinterpretPtr(fieldAddr);
  		  addr[0] = self.checkCast(value);
    	}

    	case TypeKind.STRUCT {
	      // TODO: Implement
    	}
    	
    	else {
	      // TODO: throw something
    	}
	  }
  }

  @Unsafe final override getPropertyValue(obj:Object, getter:Address[void]) -> Object {
    switch typeKind {
    	case TypeKind.CLASS
    	case TypeKind.INTERFACE {
    		let getterFn:static fn (obj:Object) -> Object = bitCast(getter);
		    return getterFn(obj);
    	}

    	else {
	      // TODO: Implement
		    return self;
    	}
	  }
  }

  @Unsafe final override setPropertyValue(obj:Object, setter:Address[void], value:Object) {
    switch typeKind {
    	case TypeKind.CLASS
    	case TypeKind.INTERFACE {
		    let setterFn:static fn (obj:Object, value:Object) = bitCast(setter);
    		setterFn(obj, self.checkCast(value));
    	}

    	else {
	      // TODO: Implement
    	}
	  }
  }
}
