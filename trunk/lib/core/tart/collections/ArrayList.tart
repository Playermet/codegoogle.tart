import tart.core.Math.max;

/** Array-backed list type. */
final class ArrayList[%ElementType] : List[ElementType] {
  private {
    var data:ElementType[];
    var _size:int;

    def grow(amount:int) {
      let nsize = self._size + amount;
      if data.size < nsize {
        let ndata = ElementType[](nsize + nsize / 2 + 16);
        ElementType[].copyElements(ndata, 0, data, 0, self._size);
        self.data = ndata;
      }

      self._size = nsize;
    }
  }

  /** Construct a new ArrayList from a variable number of input arguments.
      Parameters:
          data: The list of values to store in the ArrayList.
          initialCapacity: This optional parameter, if present, indicates
              how much initial space to reserve.
   */
  def construct(data:ElementType...; initialCapacity:int = 0) {
    initialCapacity = max(initialCapacity, data.size);
    self.data = ElementType[](initialCapacity);
    ElementType[].copyElements(self.data, 0, data, 0, data.size);
    _size = data.size;
  }

  /** Construct a new ArrayList from an array.
      Parameters:
          data: The array of values to store in the ArrayList.
          initialCapacity: This optional parameter, if present, indicates
              how much initial space to reserve.
   */
  def construct(data:Array[ElementType], initialCapacity:int = 0) {
    initialCapacity = max(initialCapacity, data.size);
    self.data = ElementType[](initialCapacity);
    ElementType[].copyElements(self.data, 0, data, 0, data.size);
    _size = data.size;
  }

  def add(e:ElementType) {
    let index = _size;
    grow(1);
    data[index] = e;
  }

  def insert(position:int, e:ElementType) {
  }

  def remove(index:int) {
    Preconditions.checkIndex(index >= 0);
    Preconditions.checkIndex(index < _size);
    ElementType[].copyElements(data, index, data, index + 1, _size - index - 1);
    --_size;
  }
  
  def clear() {
    _size = 0;
  }

  def [index:int]:ElementType {
    get {
      Preconditions.checkIndex(index >= 0);
      Preconditions.checkIndex(index < _size);
      return data[index];
    }

    set {
      Preconditions.checkIndex(index >= 0);
      Preconditions.checkIndex(index < _size);
      data[index] = value;
    }
  }
  
  def size:int {
    get { return _size; }
  }

  /** The amount of space currently reserved. */
  def capacity:int {
    get { return data.size; }
  }

  def iterate -> Iterator[ElementType] {
    return ArrayListIterator(self);
  }

  /** Iterator class for ArrayList. */
  private final class ArrayListIterator : Iterator[ElementType], HasLength {
    private var list:ArrayList;
    private var index:int;
    
    def construct(list:ArrayList) {
      self.list = list;
      self.index = 0;
    }
    
    def next -> ElementType or void {
      if (self.index < self.list.size) {
        return self.list.data[self.index++];
      } else {
        return;
      }
    }

    def length:int { get { return list.size; } }
  }
}
